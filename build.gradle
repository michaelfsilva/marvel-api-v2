buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8'
    }
}

plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.3.0'
}

apply plugin: 'org.sonarqube'

group = 'com.marvel.api'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
description = 'New version of Marvel API to provide Marvel Comics data'

sonarqube {
    properties {
        property "sonar.exclusions", ['**/com/marvel/api/config/**',
                                      '**/com/marvel/api/entity/**']
    }
}

jacoco {
    reportsDir = file("$buildDir/customJacocoReportDir")
}
jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/com/marvel/api/config/**',
                              '**/com/marvel/api/entity/**'])
        }))
    }
}
project.tasks["sonarqube"].dependsOn "jacocoTestReport"
check.dependsOn jacocoTestReport

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set("modelMapperVersion", "2.3.7")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb',
            'org.springframework.boot:spring-boot-starter-security',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-validation'
    compile 'io.springfox:springfox-swagger2:2.9.2',
            'io.springfox:springfox-swagger-ui:2.9.2',
            "org.modelmapper:modelmapper:${modelMapperVersion}",
            'io.jsonwebtoken:jjwt:0.9.1',
            'javax.xml.bind:jaxb-api:2.1'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.security:spring-security-test',
            'br.com.six2six:fixture-factory:3.1.0'
    testCompile 'org.spockframework:spock-core:2.0-M3-groovy-2.5',
            'org.spockframework:spock-spring:2.0-M3-groovy-2.5',
            'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0',
            'io.rest-assured:rest-assured:3.0.0'
}

test {
    useJUnitPlatform()
}

bootRun {
    systemProperty 'spring.profiles.active', project.hasProperty('env') ? env : 'local'
//    if (project.hasProperty('env') == 'local') {
//        systemProperty 'useLocalDatabase', true
//    }
}

test {
    systemProperty 'spring.profiles.active', project.hasProperty('env') ? env : 'local'
}

apply from: rootProject.file('gradle/install-git-hooks.gradle')
